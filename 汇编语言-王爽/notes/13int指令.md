## 13.1 int 指令

int指令的格式为：intn, n为中断类型码，它的功能是引发中断过程。
CPU执行intn指令，相当于引发一个n号中断的中断过程，执行过程如下。

1. 取中断类型码n;
2. 标志寄存器入栈，IF=0, TF=0； 
3. CS、IP入栈；
4. `(IP)=(n*4), (CS)=(n*4+2)`

int指令的最终功能和call指令相似，都是调用一段程序。

## 13.2编写供应用程序调用的中断例程

一般情况下，系统将一些具有一定功能的子程序，以中断处理程序的方式提供给应用程序调用。我们在编程的时候，可以用int指令调用这些子程序。当然，也可以自己编写一些中断处理程序供别人使用。以后，我们可以将中断处理程序简称为中断例程。

问题一：编写、安装中断7ch的中断例程。

功能：求一 word型数据的平方。
参数：(ax)=要计算的数据。
返回值：dx、ax中存放结果的高16位和低16位。
应用举例：求2*3456^2^。

```assembly
assume cs:code
code segment
start:
mov ax,3456
int 7ch		;调用中断7ch的中断例程，计算ax中的数据的平方
add ax,ax
adc dx,dx	;dx: ax存放结果，将结果乘以2 ,ADC执行加法运算时，会将CF位的值一起加到目标操作数中
mov ax,4c00h
int 21h
code ends
end start


```

分析一下，我们要做以下3部分工作。
(1) 编写实现求平方功能的程序；
(2) 安装程序，将其安装在0:200处；
(3) 设置中断向量表，将程序的入口地址保存在7ch表项中，使其成为中断7ch的中断例程。

安装程序如下。

```assembly
assume cs:code
code segment
start: 
mov ax,cs
mov ds,ax
mov si,offset sqr
mov ax,0
mov es,ax
mov di,200h
mov cx,offset sqrend-offset sqr
cld
rep movsb
mov word ptr es:[7ch*4],200h
mov word ptt es:[7ch*4+2],0
mov ax,4c00h
int 21h
sqr:
mul ax
iret
sqrend: nop
code ends
end start
```

注意，在中断例程sqr的最后，要使用iret指令。用汇编语法描述，iret指令的功能为：
`pop IP`
`pop CS`
`popf`

因为CPU执行int 7ch指令进入中断例程之前，标志寄存器、当前的CS和IP被压入栈中，



在中断例程中如果用到了寄存器si和ex等等,编写中断例程和编写子程序的时候具有同样的问题，就是要避免寄存器的冲突。应该注意例程中用到的寄存器的值的保存和恢复（用栈）。



## 13.3对int、iret和栈的深入理解

问题：用7ch中断例程完成loop指令的功能。

loop s的执行需要两个信息，循环次数和到s的位移，所以，7ch中断例程要完成loop指令的功能，也需要这两个信息作为参数。我们用cx存放循环次数，用bx存放位移。

应用举例：在屏幕中间显示80个 ！ 

```assembly
assume cs:code
code segment
start:mov ax,0b800h
mov es,ax
mov di,160*12
mov bx, offset s-offset se ;设置从标号se到标号s的转移位移
mov cx,80
s : mov byte ptr es:[di],'!'
add di,2
int 7ch ;如果(cx) !=0,转移到标号s处
se:nop
mov ax,4c00h
int 21h
code ends
end start
```

在上面的程序中，用int7ch调用7ch中断例程进行转移，用bx传递转移的位移。
分析：为了模拟loop指令，7ch中断例程应具备下面的功能。

(1) dec cx；
(2) 如果(cx)!=0,转到标号s处执行，否则向下执行。

下面我们分析7ch中断例程如何实现到目的地址的转移。
(1) 转到标号s显然应设(CS)=标号s的段地址，(IP)=标号s的偏移地址。
(2) 那么，中断例程如何得到标号s的段地址和偏移地址呢？

int 7ch引发中断过程后，进入7ch中断例程，在中断过程中，当前的标志寄存器、CS和IP都要压栈，此时压入的CS和IP中的内容，分别是调用程序的段地址(可以认为是标号s的段地址)和int 7ch**后一条指令**的偏移地址(即标号se的偏移地址)。

可见，在中断例程中，可以从栈里取得标号s的段地址和标号se的偏移地址，而用标号se的偏移地址加上bx中存放的转移位移就可以得到标号s的偏移地址。

(3)现在知道，可以从栈中直接和间接地得到标号s的段地址和偏移地址，那么如何用它们设置CS:IP呢？

可以利用iret指令，我们将栈中的se的偏移地址加上bx中的转移位移，则栈中的se的偏移地址就变为了 s的偏移地址。我们再使用iret指令，用栈中的内容设置CS、IP,从而实现转移到标号s处。

7ch中断例程如下。

```assembly
lp: 
push bp
mov bp,sp
dec cx
jexz lpret
add [bp+2],bx
lpret: 
pop bp
iret
```

因为要访问栈，使用了 bp，在程序开始处将bp 入栈保存，结束时出栈恢复。当要修改栈中se的偏移地址的时候，栈中的情况为：栈顶处是bp原来的数值，下面是se的偏移地址，再下面是s的段地址，再下面是标志寄存器的值。而此时，bp中为栈顶的偏移地址，所以((ss)*16+(bp)+2)处为se的偏移地址，将它加上bx中的转移位移就变为s的偏移地址。最后用iret出栈返回，CS:IP即从标号s处开始执行指令。

如果(cx)=0,则不需要修改栈中se的偏移地址，直接返回即可。CPU从标号se处向下执行指令。



## 13.4 BIOS和DOS所提供的中断例程

在系统板的ROM中存放着一套程序，称为BIOS(基本输入输出系统)，BIOS中主要包含以下几部分内容。

(1) 硬件系统的检测和初始化程序；
(2) 外部中断和内部中断的中断例程；
(3) 用于对硬件设备进行I/O操作的中断例程；
(4) 其他和硬件系统相关的中断例程。

操作系统DOS也提供了中断例程，从操作系统的角度来看，DOS的中断例程就是操作系统向程序员提供的编程资源。
BIOS和DOS在所提供的中断例程中包含了许多子程序，这些子程序实现了程序员在编程的时候经常需要用到的功能。程序员在编程的时候，可以用int指令直接调用BIOS和DOS提供的中断例程，来完成某些工作。

和硬件设备相关的DOS中断例程中，一般都调用了 BIOS的中断例程。

## 13.5 BIOS和DOS中断例程的安装过程

(1) 开机后，CPU 一加电，初始化(CS)=0FFFFH, (IP)=0,自动从FFFF:0单元开始执行程序。FFFF:0处有一条转跳指令，CPU执行该指令后，转去执行BIOS中的硬件系统检测和初始化程序。
(2) 初始化程序将建立BIOS所支持的中断向量，即将BIOS提供的中断例程的入口地址登记在中断向量表中。注意，对于BIOS所提供的中断例程，只需将入口地址登记在中断向量表中即可，因为它们是固化到ROM中的程序，一直在内存中存在。
(3) 硬件系统检测和初始化完成后，调用int 19h进行操作系统的引导。从此将计算机交由操作系统控制。
(4) DOS启动后，除完成其他工作外，还将它所提供的中断例程装入内存，并建立相应的中断向量。


## 13.6 BIOS中断例程应用

不得不说，还是王爽设置的知识合理并且挺有趣的

int 10h中断例程是BIOS提供的中断例程，其中包含了多个和屏幕输出相关的子程序。

一般来说，一个供程序员调用的中断例程中往往包括多个子程序，中断例程内部用传递进来的参数来决定执行哪一个子程序。BIOS和DOS提供的中断例程，都用ah来传递内部子程序的编号。

下面看一下int 10h中断例程的设置光标位置功能。

```assembly

mov ah, 2 ;置光标
mov bh, 0 ;第0页
mov dh,5 ;dh中放行号
mov dl,12 ;dl中放列号
int 10h

```

(ah)=2表示调用第10h号中断例程的2号子程序，功能为设置光标位置，可以提供光标所在的行号(80 * 25字符模式下：0〜24)、列号(80*25字符模式下：0〜79),和页号作为参数。(bh)=0, (dh)=5, (dl)=12,设置光标到第0页，第5行，第12列。



bh中页号的含义：内存地址空间中，B8000H〜BFFFFH共32kB的空间，为80*25彩色字符模式的显示缓冲区。
一屏的内容在显示缓冲区中共占4000个字节。

显示缓冲区分为8页，每页4KB,显示器可以显示任意一页的内容。一般情况下，显示第0页的内容。也就是说，通常情况下，B8000H〜B8F9FH中的4000个字节的内容将出现在显示器上。



再看一下int 10h中断例程的在光标位置显示字符功能。

```assembly

mov ah, 9 ;在光标位置显示字符
mov al,'a' ;字符
mov bl,7 ;颜色属性
mov bh, 0 ;第0页
mov cx, 3 ;字符重复个数
int 1Oh

```

(ah)=9表示调用第10h号中断例程的9号子程序，功能为在光标位置显示字符，可以提供要显示的字符、颜色属性、页号、字符重复个数作为参数。

bl中的颜色属性的格式如下。

![image-20210127111730642](images/13/image-20210127111730642.png)

可以看出，和显存中的属性字节的格式相同。

编程：在屏幕的5行12列显示3个红底高亮闪烁绿色的’a'。



## 13.7 DOS中断例程应用

int 21h中断例程是DOS提供的中断例程，其中包含了 DOS提供给程序员在编程时调用的子程序。

我们前面一直使用的是int21h中断例程的4ch号功能，即程序返回功能，如下：

```assembly
mov ah, 4ch ;程序返回
mov al, 0 ;返回值
int 21h
```

(ah)=4ch表示调用第21h号中断例程的4ch号子程序，功能为程序返回，可以提供返回值作为参数。
我们前面使用这个功能的时候经常写做：
`mov ax,4c00h`
`int 21h`

我们看一下int21h中断例程在光标位置显示字符串的功能：

```assembly
ds:dx 指向字符串 ;要显示的字符串需用"$"作为结束符
mov ah, 9 ;功能号9，表示在光标位置显示字符串
int 21h
```

(ah)=9表示调用第21h号中断例程的9号子程序，功能为在光标位置显示字符串，可以提供要显示字符串的地址作为参数。

编程：在屏幕的5行12列显示字符串“Welcome to masm!”。

```assembly
assume cs:code
data segment
db 'Welcome to masm','$'
data ends
code segment
start:
mov ah, 2 ;置光标
mov bh,0 ;第0页
mov dh,5 ;dh中放行号
mov dl,12 ;dl中放列号
int 10h
mov ax,data
mov ds,ax
mov dx,0 ; ds:dx指向字符串的首地址data:0
mov ah,9
int 21h
mov ax,4c00h
int 21h
code ends
end start 

```

