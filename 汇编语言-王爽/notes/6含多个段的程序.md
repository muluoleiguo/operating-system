程序取得所需空间的方法有两种，
一是在加载程序的时候为程序分配，再就是程序在执行的过程中向系统申请（略）。

## 6.1在代码段中使用数据

编程计算以下8个数据的和，结果存在ax寄存器中

0123h、0456h、0789h、0abch、0defh、0fcdh、0cbah、0987h

```assembly
assume cs:code
code segment
	dw 0123h, 0456h, 0789h,0abch,0defh,0fedh,0cbah, 0987h
    mov bx,0
    mov ax,0
    mov cx,8
  s:add ax,cs:[bx]
    add bx,2
    loop s
    mov ax,4c00h
    int 21h
code ends
end

```

程序第一行中的“dw”的含义是定义字型数据。dw即“define word”
使用dw定义了 8个字型数据(数据之间以逗号分隔)，它们所占的内存空间的大小为16个字节。

程序中的指令就要对这8个数据进行累加，可这8个数据在哪里呢？由于它们在代码段中，程序在运行的时候CS中存放代码段的段地址，所以可以从CS中得到它们的段地址。它们的偏移地址是多少呢？因为用dw定义的数据处于代码段的最开始，所以偏移地址为0,这8个数据就在代码段的偏移0、2、4、6、8、A、C、E处。程序运行时，它们的地址就是 CS:0、CS:2、CS:4、CS:6、CS:8、CS:A、CS:C、CS:E。

用bx存放加2递增的偏移地址



怎样执行程序中的指令呢？用Debug加载后，可以将IP设置为10h,从而使CS:IP指向程序中的第一条指令。然后再用t命令、p命令，或者是g命令执行。

可是这样一来，我们就必须用Debug来执行程序

如何让这个程序在编译、连接后可以在系统中直接运行呢？我们可以在源程序中指明程序的入口所在，

```assembly
assume cs:code
code segment
    dw 0123h, 0456h, 0789h,0abch,0defh,0fedh,0cbah, 0987h
    start:  mov bx,0
            mov ax,0
            mov cx,8
    s: 		add ax,cs:[bx]
            add bx,2
            loop s
            mov ax,4c00h
            int 21h
code ends
end start
```

在程序的第一条指令的前面加上了一个标号start,而这个标号在伪指令end的后面出现。这里，我们要再次探讨end的作用。end除了通知编译器程序结束外，还可以**通知编译器程序的入口在什么地方**。



## 6.2在代码段中使用栈

利用栈，将前面的程序中定义的数据逆序存放。

实现思路：程序运行时，定义的数据存放在cs:0~cs:F单元中，共8个字单元。依次将这8个字单元中的数据入栈，然后再依次出栈到这8个字单元中，从而实现数据的逆序存放。



我们首先要有一段可当作栈的内存空间（由系统来分配）

```assembly
assume cs:codesg
codesg segment
    dw 0123h,0456h, 0789h,0abch,0defh,0fedh,0cbah, 0987h
    dw 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
    ;用dw定义16个字型数据，在程序加载后，将取得16个字的内存空间，
    ;存放这16个数据。在后面的程序中将这段空间当作栈来使用
    start:  mov ax,cs
            mov ss,ax
            mov sp,30h	 ;将设置栈顶ss : sp指向cs : 30
            mov bx, 0
            mov cx,8
    s : 	push cs: [bx]
            add bx,2
            loop s		;以上将代码段0〜15单元中的8个字型数据依次入栈
            
            mov bx, 0
            mov cx,8
    s0: 	pop cs:[bx]
            add bx,2
            loop s0		 ;以上依次出栈8个字型数据到代码段0〜15单元中
            mov ax,4c00h
            int 21h
   
 
codesg ends
end start ;指明程序的入口在start处

```



我们要将cs:10~cs:2F的内存空间当作栈来用，初始状态下栈为空，所以ss:sp要指向栈底，则设置ss:sp指向cs:30



## 6.3将数据、代码、栈放入不同的段

```assembly
assume cs:code,ds:data,ss:stack
data segment
	dw 0123h,0456h, 0789h,0abch,0defh,0fedh,0cbah, 0987h
data ends
stack segment
	dw 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
stack ends
code segment
start:  mov ax, stack
        mov ss,ax
        mov sp,20h  ;设置栈顶ss: sp指向stack:20
        mov ax,data
        mov ds,ax	;ds指向data段
        mov bx,0
        mov cx,8
s: 		push [bx]
        add bx,2
        loop s		;以上将data段中的0〜15单元中的8个字型数据依次入栈
        mov bx, 0
        mov cx,8
s0: 	pop [bx]
        add bx, 2
        loop s0		;以上依次出栈8个字型数据到data段的0~15单元中
        mov ax,4c00h
        int 21h
code ends
end start
```

1. 定义多个段的方法这点，我们从程序中可明显地看出，定义一个段的方法和前面所讲的定义代码段的方
   法没有区别，只是对于不同的段，要有不同的段名。

2. 如何访问段中的数据呢？当然要通过地址，而地址是分为两部分的，即段地址和偏移地址

```assembly
mov ax,data		;不能直接mov ds,data  8086数值不能直接入段寄存器
mov ds,ax
mov bx,ds:[6]
```

3. “代码段”、“数据段”、“栈段”完全是我们自己的安排

